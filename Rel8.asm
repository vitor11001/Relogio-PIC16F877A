	PROCESSOR	16F877A
	RADIX		DEC

#INCLUDE <P16F877A.inc>
	__CONFIG	0X3F32

	ORG	0X00
	GOTO INICIO

	ORG 0X04
	GOTO INTER

	ORG	0X20
TEMP	RES	2 ;REGISTRADOR TEMPORARIO
SEGU	RES 1 ;UNIDADE DE SEG
SEGD	RES 1 ;DEZENA DE SEG
MINU	RES	1 ;UNIDADE DE MIN
MIND	RES 1 ;DEZENA DE MIN
HORAU	RES	1 ;UNIDADE DE HORA
HORAD	RES	1 ;DEZENA DE HORA
W_TEMP	RES 1 ;SALVA W
S_TEMP	RES 1 ;SALVA STATUS
FLAGS	RES 1 ;0-PONTO,1-HORAMIN E MINSEG,2-MOSTRA CRONOMETRO
			  ;3-CRONOMETRO PARA/CONT
DEZSEG	RES	1 ;DEZENAS DE SEG PARA O RELOGIO
CRODEZ	RES	1 ;CRONOMETRO DEZENAS DE SEGUNDOS
CROU	RES	1 ;CRONOMETRO UNIDADE DE SEGUNDOS
CROD	RES 1 ;CRONOMETRO DEZENAS DE SEGUNDOS
CROC	RES 1 ;CRONOMETRO CENTENAS DE SEGUNDOS

INICIO
	BANKSEL	TRISD
	MOVLW	00000000B
	MOVWF	TRISD
	MOVWF	TRISA
	MOVLW	11111111B
	MOVWF	TRISB
	MOVLW	0X06
	MOVWF	ADCON1
	BSF		PIE1, TMR1IE ;LIGA INT. TIMER
	BSF		INTCON, PEIE ;LIGA INT. PERIFERICOS
	BSF		INTCON, GIE ;LIGA INT. GERAL

	BANKSEL	PORTD
	MOVLW	00000000B
	MOVWF	PORTD
	MOVWF	PORTA
	CLRF	T1CON

	BSF		T1CON,TMR1ON ;LIGA TMR1
	BSF		T1CON,T1CKPS1
	BSF		T1CON,T1CKPS0

	CLRF	SEGU
	CLRF	SEGD
	CLRF	MINU
	CLRF	MIND
	CLRF	HORAU
	CLRF	HORAD
	CLRF	TMR1L
	CLRF	TMR1H
	CLRF	FLAGS
	CLRF	CRODEZ
	CLRF	CROU
	CLRF	CROD
	CLRF	CROC
	CLRF	DEZSEG
	BSF		FLAGS,1

LOOP

;***************BOTAO0********************
	BTFSC 	PORTB,0
	GOTO 	$+14
	BTFSS 	PORTB,0
	GOTO 	$-1
	BTFSS 	FLAGS,2
	GOTO 	$+6
	CLRF 	CRODEZ
	CLRF 	CROU
	CLRF 	CROD
	CLRF 	CROC
	GOTO 	$+5

;************AJUSTA O TEMPO****************
    MOVLW 	00000110B;0x600
    MOVWF 	PCLATH
    CALL 	AJUSTE
    CLRF 	PCLATH
;******************************************
;******************************************

;***************BOTAO3*********************
	MOVLW	00000010B
	BTFSC	PORTB,3
	GOTO	$+4
	BTFSS	PORTB,3
	GOTO	$-1
	XORWF	FLAGS,F
;*****************************************

;******************BOTAO4*****************
    MOVLW	00000100B
    BTFSC	PORTB,4
    GOTO	$+4
    BTFSS 	PORTB,4
    GOTO 	$-1
    XORWF 	FLAGS,F
;*****************************************

;******************BOTAO5*****************
    MOVLW 	00001000B
    BTFSC 	PORTB,5
    GOTO 	$+4
    BTFSS 	PORTB,5
    GOTO 	$-1
    XORWF 	FLAGS,F
;******************************************

;*CHAMA A FUNÇAO PARA INCREMENTAR O TEMPO**
	MOVLW	00000100B
	MOVWF	PCLATH
	CALL	TIMER
	CLRF	PCLATH
;******************************************

;*******CHAMA O LIGA DISPLAY***************
	MOVLW	00000010B
	MOVWF	PCLATH
	CALL	DISPLAY
	CLRF	PCLATH
;******************************************

	GOTO	LOOP

;*TEMPO QUE OS DISPLAYS PERMANECEM ACESSOS*
ATRASO2 ;X*10+7
	MOVLW	100/256+1
	MOVWF	TEMP
	MOVLW	100%256
	MOVWF	TEMP+1

	NOP
	NOP
	NOP
	NOP
	NOP

	DECF	TEMP+1,F
	BTFSC	STATUS,Z

	DECFSZ	TEMP,F
	GOTO	$-8

	RETURN
;******************************************

;**************LIGA O DISPLAY**************
	ORG	0X200
DISPLAY
	BCF		PORTA,2; DESLIGA DISPLAY 4
	BSF		PORTA,5; LIGA DISPLAY 1
	MOVLW	00000101B; 0x500
	MOVWF	PCLATH
	MOVF	MINU,W
	BTFSS	FLAGS,1
	MOVF	SEGU,W
	BTFSC	FLAGS,2
	RRF		CRODEZ,W
	CALL	HEX7SEG
	MOVWF	PORTD
	CLRF	PCLATH
    CALL	ATRASO2

    MOVLW	00000101B
    MOVWF	PCLATH
	BCF		PORTA,5; DESLIGA DISPLAY 1
	BSF		PORTA,4; LIGA DISPLAY 2
    MOVF	MIND,W
    BTFSS	FLAGS,1
    MOVF	SEGD,W
    BTFSC	FLAGS,2
    MOVF	CROU,W;
    CALL	HEX7SEG
    MOVWF	PORTD
    BTFSC	FLAGS,2
    BSF		PORTD,7
    CLRF	PCLATH
    CALL	ATRASO2

    MOVLW 	00000101B
    MOVWF 	PCLATH
	BCF		PORTA,4; DESLIGA DISPLAY 2
	BSF		PORTA,3; LIGA DISPLAY 3
    MOVF 	HORAU,W
    BTFSS 	FLAGS,1
    MOVF 	MINU,W
    BTFSC 	FLAGS,2
    MOVF 	CROD,W
    CALL 	HEX7SEG
    MOVWF 	PORTD
    BTFSC 	FLAGS,2
    GOTO 	$+7
	BTFSS	FLAGS,1
	GOTO	$+5
    BTFSC 	FLAGS,0
    BSF 	PORTD,7
    BTFSS 	FLAGS,0
    BCF 	PORTD,7
    CLRF 	PCLATH
    CALL 	ATRASO2

    MOVLW 	00000101B
    MOVWF 	PCLATH
	BCF		PORTA,3; DESLIGA DISPLAY 3
	BSF		PORTA,2; LIGA DISPLAY 4
    MOVF 	HORAD,W
    BTFSS 	FLAGS,1
    MOVF 	MIND,W
    BTFSC 	FLAGS,2
    MOVF 	CROC,W
    CALL 	HEX7SEG
    MOVWF 	PORTD
    CLRF 	PCLATH
    CALL 	ATRASO2

	RETURN
;****************************************

;********INTERRUPÇAO DO TIMER************
	ORG	0X300
INTER
	MOVWF	W_TEMP; SALVA CONTEXTO
	SWAPF	STATUS,W
	MOVWF	S_TEMP

	BCF		PIR1,TMR1IF
	INCF	DEZSEG,F
	NOP
	NOP
	NOP
	
	MOVLW	59288/256; 
	MOVWF	TMR1H
	MOVLW	59288%256; 
	MOVWF	TMR1L

;****************************************
    BTFSC	FLAGS,3; CRONOMETRO SE FLAGS 3 FOR 1 N INC
    INCF	CRODEZ,F; SE FLAGS 3 FOR 0 INCREMENTANDO
;****************************************

	SWAPF	S_TEMP,W
	MOVWF	STATUS
	SWAPF	W_TEMP,F
	SWAPF	W_TEMP,W

	RETFIE; RETORNO PARA INTERRUPÇOES
;****************************************

;***********CONTAGEM DO TIMER************
    Org 0x0400

TIMER

    BCF 	STATUS,C
    RRF 	DEZSEG,W
    XORLW 	10
    BTFSS 	STATUS,Z
    GOTO 	$+5
    CLRF 	DEZSEG
    INCF 	SEGU,F
	MOVLW	00000001B
	XORWF	FLAGS,F; FAZ O PONTO PISCAR

    MOVF 	SEGU,W
    XORLW 	10
    BTFSS 	STATUS,Z
    GOTO 	$+3
    CLRF 	SEGU
    INCF 	SEGD,F

    MOVF 	SEGD,W
    XORLW 	6
    BTFSS 	STATUS,Z
    GOTO 	$+3
    CLRF 	SEGD
    INCF 	MINU,F

    MOVF 	MINU,W
    XORLW 	10
    BTFSS 	STATUS,Z
    GOTO 	$+3
    CLRF 	MINU
    INCF 	MIND,F

    MOVF 	MIND,W
    XORLW 	6
    BTFSS 	STATUS,Z
    GOTO 	$+3
    CLRF 	MIND
    INCF 	HORAU,F

    MOVF 	HORAU,W
    XORLW 	10
    BTFSS 	STATUS,Z
    GOTO 	$+3
    CLRF 	HORAU
    INCF 	HORAD,F

    MOVF 	HORAD,W
    XORLW 	2
    BTFSS 	STATUS,Z
    GOTO 	$+7
    MOVF 	HORAU,W
    XORLW 	4
    BTFSS 	STATUS,Z
    GOTO 	$+3
    CLRF 	HORAD
    CLRF 	HORAU

;**************CRONOMETRO****************
    BCF 	STATUS,C
    RRF 	CRODEZ,W
    XORLW 	10
    BTFSS 	STATUS,Z
    GOTO 	$+3
    CLRF 	CRODEZ
    INCF 	CROU,F

    MOVF 	CROU,W
    XORLW 	10
    BTFSS 	STATUS,Z
    GOTO 	$+3
    CLRF 	CROU
    INCF 	CROD,F

    MOVF 	CROD,W
    XORLW 	10
    BTFSS 	STATUS,Z
    GOTO 	$+3
    CLRF 	CROD
    INCF 	CROC,F

    MOVF 	CROC,W
    XORLW 	10
    BTFSC 	STATUS,Z
    CLRF 	CROC

    RETURN
;****************************************

;****************HEX7SEG*****************
	ORG	0X500
HEX7SEG
	ANDLW	00001111B
	ADDWF	PCL,F
		   ;PGFEDCBA
	RETLW	00111111B ;0
	RETLW	00000110B ;1
	RETLW	01011011B ;2
	RETLW	01001111B ;3
	RETLW	01100110B ;4
	RETLW	01101101B ;5
	RETLW	01111101B ;6
	RETLW	00000111B ;7
	RETLW	01111111B ;8
	RETLW	01101111B ;9
	RETLW	01110111B ;A
	RETLW	01111100B ;B
	RETLW	00111001B ;C
	RETLW	01011110B ;D
	RETLW	01111001B ;E
	RETLW	01110001B ;F
;****************************************

;****************AJUSTE******************
	ORG	0X600
	
AJUSTE
	BCF		T1CON,0
	BSF		PORTA,2
	
@1	MOVLW	00000101
	MOVWF	PCLATH
	BTFSC	PORTA,2
	MOVF	HORAD,W
	BTFSC	PORTA,3
	MOVF	HORAU,W
	BTFSC	PORTA,4
	MOVF	MIND,W
	BTFSC	PORTA,5
	MOVF	MINU,W
	CALL	HEX7SEG
	MOVWF	PORTD
	MOVF	TEMP,W
	MOVWF	PCLATH

;****************BOTAO1******************
;**************INCREMENTA****************
	BTFSC	PORTB,1
	GOTO	$+11

	BTFSS	PORTB,1
	GOTO	$-1
	BTFSC	PORTA,2
	INCF	HORAD,F
	BTFSC	PORTA,3
	INCF	HORAU,F
	BTFSC	PORTA,4
	INCF	MIND,F
	BTFSC	PORTA,5
	INCF	MINU,F
;****************************************

;****************BOTAO2******************
;**************DECREMENTA****************
	BTFSC	PORTB,2
	GOTO	$+11

	BTFSS	PORTB,2
	GOTO	$-1
	BTFSC	PORTA,2
	DECF	HORAD,F
	BTFSC	PORTA,3
	DECF	HORAU,F
	BTFSC	PORTA,4
	DECF	MIND,F
	BTFSC	PORTA,5
	DECF	MINU,F
;****************************************

;*******CONFERE O VALOR DO TIMER*********
;*************DE INC E DEC***************
	MOVF	MINU,W
	XORLW	10
	BTFSC	STATUS,Z
	CLRF	MINU

	MOVF	MIND,W
	XORLW	6
	BTFSC	STATUS,Z
	CLRF	MIND

	MOVF	HORAD,W
	XORLW	3
	BTFSC	STATUS,Z
	CLRF	HORAD

	MOVF	HORAD,W
	XORLW	2
	BTFSS	STATUS,Z
	GOTO	$+6
	
	MOVF	HORAU,W
	XORLW	4
	BTFSC	STATUS,Z
	CLRF	HORAU
	GOTO	$+5

	MOVF	HORAU,W
	XORLW	10
	BTFSC	STATUS,Z
	CLRF	HORAU

;****************************************
	MOVF	MINU,W
	XORLW	11111111B;
	BTFSS	STATUS,Z
	GOTO	$+3
	MOVLW	9
	MOVWF	MINU

	MOVF	MIND,W
	XORLW	11111111B
	BTFSS	STATUS,Z
	GOTO	$+3
	MOVLW	5
	MOVWF	MIND

	MOVF	HORAD,W
	XORLW	11111111B
	BTFSS	STATUS,Z
	GOTO	$+3
	MOVLW	2
	MOVWF	HORAD

	MOVF	HORAD,W
	XORLW	2
	BTFSS	STATUS,Z
	GOTO	$+8

	MOVF	HORAU,W
	XORLW	11111111B
	BTFSS	STATUS,Z
	GOTO	$+3
	MOVLW	3
	MOVWF	HORAU
	GOTO	$+7

	MOVF	HORAU,W
	XORLW	11111111B
	BTFSS	STATUS,Z
	GOTO 	$+3
	MOVLW	9
	MOVWF	HORAU

;****************BOTAO0******************
	BTFSC	PORTB,0
	GOTO	$+23
	BTFSS 	PORTB,0
	GOTO 	$-1

;****************************************
;CASO A HORAD SEJA 2 E A HORAU ESTAJA MAIOR QUE 3
	BTFSS	PORTA,2;
	GOTO	$+9
	MOVF	HORAD,W
	XORLW	2
	BTFSS	STATUS,Z
	GOTO	$+5
	MOVF	HORAU,W
	SUBLW	4
	BTFSS	STATUS,C
	CLRF	HORAU
;****************************************

	BCF 	STATUS,C
	RLF 	PORTA,F

	BTFSS 	PORTA,6
	GOTO	$+7
	BSF		T1CON,0
	MOVLW	0
	MOVWF	DEZSEG
	MOVWF	SEGU
	MOVWF	SEGD
	
	RETURN	
	
	GOTO 	@1
;****************************************

	END